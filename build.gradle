apply plugin: 'com.android.application'
apply plugin: 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

def SigningProperty = "BookCatalogue.signing"
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def BcSigning = properties.getProperty(SigningProperty)

if (BcSigning != null && (new File(BcSigning)).exists()) {
    apply from: BcSigning
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.secrets.gradle.plugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

dependencies {
    implementation libs.androidx.legacy.support.v4
    implementation libs.androidx.appcompat
    // Compile all libraries from 'libs' folder
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation libs.material
    implementation libs.play.services.vision
    implementation libs.androidx.room.common.jvm
    implementation libs.firebase.firestore
    implementation libs.androidx.room.runtime
    implementation libs.androidx.credentials
    implementation libs.androidx.credentials.play.services.auth
    implementation libs.googleid
    annotationProcessor libs.androidx.room.compiler
    //ACRA Core for crash alerts
    implementation libs.acra.core
    implementation libs.acra.mail
    implementation libs.acra.toast
    implementation libs.acra.dialog
}

android {
    namespace = 'com.eleybourn.bookcatalogue'
    buildTypes {
        release {
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GOOGLE_OAUTH_CLIENT_ID", properties.getProperty("GOOGLE_OAUTH_CLIENT_ID", "\"\"")
            //ndk {
            //    debugSymbolLevel 'SYMBOL_TABLE'
            //}
        }
        debug {
            buildConfigField "String", "GOOGLE_OAUTH_CLIENT_ID", properties.getProperty("GOOGLE_OAUTH_CLIENT_ID", "\"\"")
        }
    }
    compileSdkVersion 36

    packagingOptions {
        resources {
            excludes += ['META-INF/NOTICE.txt', 'META-INF/LICENSE.txt']
        }
    }
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:deprecation']
    }
    // !!! Commented to use values from the manifest
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 36
    }

    flavorDimensions "distributor"
    productFlavors {
        distPlay {
            // Assigns this product flavor to the "distributor" flavor dimension.
            // This property is optional if you are using only one dimension.
            dimension "distributor"
            versionNameSuffix "-distplay"
            buildConfigField 'boolean', 'IS_DONATE_ALLOWED', 'false'
        }
        distOther {
            dimension "distributor"
            versionNameSuffix "-distoth"
            buildConfigField 'boolean', 'IS_DONATE_ALLOWED', 'false'
        }
    }
    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }
    lint {
        disable 'MissingTranslation'
    }
    buildFeatures {
        renderScript = true
        aidl = true
        viewBinding = false
        buildConfig = true
    }
}

secrets {
    // Change the properties file from the default "local.properties" in your root project
    // to another properties file in your root project.
    propertiesFileName = 'secrets.properties'
}
